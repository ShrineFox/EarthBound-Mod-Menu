import keyitems
import "../../_scripts.ccs"
import "../modmenu/modmenu.ccs"
import "../modmenu/config/configmenu.ccs"
/* ASM */
import "../asm/asm65816.ccs"
import "../asm/cc_main_menu.ccs"
//import "../asm/movement_reloc.ccs"
/* Enums */
import "../enums/windows.ccs"
import "../enums/labels.ccs"
import "../enums/flags.ccs"
/* Commands */
import "../commands/window_cmds.ccs"
import "../commands/flag_cmds.ccs"
import "../commands/health_cmds.ccs"


// Open custom menu instead of regular one, then forcibly close the text + HP windows
ROM[0xc134af] = { MText(MainMenu) NOP NOP NOP NOP NOP NOP 
NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP NOP 
JSR(0xC10A1D) JMP(0xC13C8C) }

Selected_Goods:   { counter(1) eob }  Selected_Equip:    { counter(2) eob }
Selected_PSI:     { counter(3) eob }  Selected_KeyItems: { counter(4) eob }
Selected_ModMenu: { counter(5) eob }  Selected_Status: { counter(6) eob }

MainMenu:
	unset(flg_RandomWarp)
    if (flg_EndLoop) { unset(flg_EndLoop) goto(goto_end) }
	//sprite_spawn(106, 896, 1) // Invisible camera sprite running "Cutscene On"
	// Stop player movement
	"[1F E5 FF]"
	// Open appropriately sized window
	window_closeall
	window_open(wnd_Action)
	Lbl(lbl_Action)
	counter_zero
	ctoarg
	// Options
	MenuOptionWithCallback("Goods", Selected_Goods)
	if flg_KeyItems { MenuOptionWithCallback("Key Items", Selected_KeyItems) }
	if flg_LearnedPSI { MenuOptionWithCallback("PSI", Selected_PSI) }
	MenuOptionWithCallback("Equip", Selected_Equip)
	if flg_ModMenu { MenuOptionWithCallback("Mod Menu", Selected_ModMenu) }
	if not flg_ModMenu { MenuOptionWithCallback("Config", Selected_ModMenu) }
	MenuOptionWithCallback("Status", Selected_Status)
	// Width
	DisplayVertical(2)
	// Exit by default
	if not "[11]" { goto(goto_end) }
	// Results
	ctoarg
    swap
	SwitchGoto(6)
        SwitchCase(goto_goods)
        SwitchCase(goto_equip)
		SwitchCase(goto_psi)
        SwitchCase(goto_keyitems)
		SwitchCase(goto_modmenu)
        SwitchCase(goto_status)
	swap
    "@Error! No option {number(0)} in this menu." goto(goto_end)
	goto_goods:
		window_closeall open_hp open_goods goto(goto_end)
	goto_equip:
		window_closeall open_equip goto(MainMenu)
	goto_psi:
		window_closeall open_hp open_psi goto(goto_end)
	goto_keyitems:
		window_closeall call(KeyItems) goto(MainMenu)
	goto_modmenu:
		window_closeall if flg_ModMenu { call(modmenu.ModMenu) } if not flg_ModMenu { call(configmenu.ConfigMenu) } goto(goto_end)
    goto_status:
		window_closeall open_status goto(MainMenu)
	goto_end:
		window_closeall
		if flg_Proceed { unset(flg_Proceed) goto(MainMenu) } // Loop Main Menu while flag is on
        //sprite_spawn(106, 897, 1) // Invisible camera sprite running "Cutscene Off"
        // Resume player movement
        "[1F ED]"
eob